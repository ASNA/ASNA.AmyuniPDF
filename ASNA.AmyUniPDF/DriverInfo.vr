Using System
Using System.Text
Using System.Xml
Using System.IO

DclNamespace ASNA.AmyUniPDF

BegClass DriverInfo Access(*Public)
    DclProp PrinterName          Type(*String) Access(*Public) SetAccess(*Private)
    DclProp LicenseCompany       Type(*String) Access(*Public) SetAccess(*Private)
    DclProp LicenseCode          Type(*String) Access(*Public) SetAccess(*Private)
    DclProp OutputPath           Type(*String) Access(*Public) SetAccess(*Private)
    DclProp OutputFileName       Type(*String) Access(*Public) SetAccess(*Private)

    BegFunc GetFullXmlFileName Type(*String) Access(*Private) Shared(*Yes)
        DclSrParm XmlFilePath  Type(*String)

        DclFld   FullXmlFileName Type(*String)
        DclConst FileName Value('AmyuniDriverInfo.xml')

        XmlFilePath = Helpers.EnsureTrailingSlash(XmlFilePath)

        FullXmlFileName = XmlFilePath + FileName
        If ( NOT File.Exists(FullXmlFileName))
            Throw *New System.Exception('Amyuni XML driver info file is not available at: ' + FullXmlFileName)
        EndIf

        LeaveSr FullXmlFileName
    EndFunc

    BegFunc GetInstance Access(*Public) Shared(*Yes) Type(DriverInfo)
        DclSrParm XmlFilePath     Type(*String)
        DclSrParm OutputFilePath  Type(*String)

        DclFld XmlDoc          Type(XmlDocument) New()
        DclFld DocRoot         Type(*String)
        DclFld Node            Type(XmlNode)
        DclFld ai              Type(DriverInfo) New()
        DclFld FullXmlFileName Type(*String)

        OutputFilePath = Helpers.EnsureTrailingSlash(OutputFilePath)

        FullXmlFileName = GetFullXmlFileName(XmlFilePath)
        XmlDoc.Load(FullXmlFileName)

        DocRoot = "/Root"
        Node = XmlDoc.SelectSingleNode(DocRoot + '/AmyuniPDF/PrinterName')
        ai.PrinterName = Node.InnerText

        Node = XmlDoc.SelectSingleNode(DocRoot + '/AmyuniPDF/LicenseCompany')
        ai.LicenseCompany = Node.InnerText

        Node = XmlDoc.SelectSingleNode(DocRoot + '/AmyuniPDF/LicenseCode')
        ai.LicenseCode = Node.InnerText

        ai.OutputFileName = Path.GetRandomFileName().Replace('.', '-')
        ai.OutputPath = OutputFilePath

        LeaveSr ai
    EndFunc
EndClass


