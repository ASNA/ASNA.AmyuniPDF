Using System
Using System.Text
Using System.Xml
Using System.IO

DclNamespace ASNA.AmyUniPDF

BegClass PdfDriverInfo Access(*Public)
    // The printer name as view in "Devices and Printers" in Windows.
    DclFld _PrinterName          Type(*String)
    // The licensed company and code from Amyuni.
    DclFld _LicenseCompany       Type(*String)
    DclFld _LicenseCode          Type(*String)
    // The output path for the PDF file.
    DclFld _OutputPath           Type(*String)
    // The PDF file name *without* the PDF extension. 
    DclFld _OutputFileName       Type(*String)

    BegProp PrinterName Type(*String) Access(*Public) SetAccess(*Private)
        BegGet
            LeaveSr *This._PrinterName
        EndGet
        BegSet
            *This._PrinterName = *PropVal
        EndSet
    EndProp

    BegProp LicenseCompany Type(*String) Access(*Public) SetAccess(*Private)
        BegGet
            LeaveSr *This._LicenseCompany
        EndGet
        BegSet
            *This._LicenseCompany = *PropVal
        EndSet
    EndProp

    BegProp LicenseCode  Type(*String) Access(*Public) SetAccess(*Private)
        BegGet
            LeaveSr *This._LicenseCode
        EndGet
        BegSet
            *This._LicenseCode  = *PropVal
        EndSet
    EndProp

    BegProp OutputPath Type(*String) Access(*Public) SetAccess(*Private)
        BegGet
            LeaveSr *This._OutputPath
        EndGet
        BegSet
            *This._OutputPath = *PropVal
        EndSet
    EndProp

    BegProp OutputFileName Type(*String) Access(*Public) SetAccess(*Private)
        BegGet
            LeaveSr *This._OutputFileName
        EndGet
        BegSet
            *This._OutputFileName = *PropVal
        EndSet
    EndProp
        
    // Given the path where the XML configuration file should be located, return 
    // the full name of the XML configuration. In this example the file name is 
    // hardcoded at `AmyuniDriverInfo.xml`'
    BegFunc GetFullXmlFileName Type(*String) Access(*Private) Shared(*Yes)
        DclSrParm XmlFilePath  Type(*String)

        DclFld   FullXmlFileName Type(*String)
        DclConst FileName Value('AmyuniDriverInfo.xml')

        XmlFilePath = Helpers.EnsureTrailingBackSlash(XmlFilePath)
        FullXmlFileName = XmlFilePath + FileName
        If ( NOT File.Exists(FullXmlFileName))
            Throw *New System.Exception('Amyuni XML driver info file is not available at: ' + FullXmlFileName)
        EndIf

        LeaveSr FullXmlFileName
    EndFunc

    // This shared method returns an instance of `PdfDriverInfo` with its properties populated.  
    BegFunc GetInstance Access(*Public) Shared(*Yes) Type(PDFDriverInfo)
        DclSrParm XmlFilePath     Type(*String)
        DclSrParm OutputFilePath  Type(*String)

        DclFld XmlDoc          Type(XmlDocument) New()
        DclFld DocRoot         Type(*String)
        DclFld Node            Type(XmlNode)
        DclFld di              Type(PDFDriverInfo) New()
        DclFld FullXmlFileName Type(*String)

        OutputFilePath = Helpers.EnsureTrailingBackSlash(OutputFilePath)

        FullXmlFileName = GetFullXmlFileName(XmlFilePath)
        XmlDoc.Load(FullXmlFileName)

        DocRoot = "/Root"
        Node = XmlDoc.SelectSingleNode(DocRoot + '/AmyuniPDF/PrinterName')
        di.PrinterName = Node.InnerText

        Node = XmlDoc.SelectSingleNode(DocRoot + '/AmyuniPDF/LicenseCompany')
        di.LicenseCompany = Node.InnerText

        Node = XmlDoc.SelectSingleNode(DocRoot + '/AmyuniPDF/LicenseCode')
        di.LicenseCode = Node.InnerText

        di.OutputFileName = Path.GetRandomFileName().Replace('.', '-')
        di.OutputPath = OutputFilePath

        LeaveSr di
    EndFunc
EndClass

